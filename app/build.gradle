apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

apply plugin: 'kotlin-kapt'
kapt {
    correctErrorTypes = true
    useBuildCache = true
}

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "com.fonteraro.martaco.cinemahub"
        minSdkVersion 17
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Default servers URL
        buildConfigField "String", "API_SERVER_URL", "\"https://movies-sample.herokuapp.com/api/\""

        // Default server endpoints
        buildConfigField "String", "MOVIES", "\"movies\""

        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility Versions.JAVA_VERSION_STR
        targetCompatibility Versions.JAVA_VERSION_STR
    }
    buildFeatures {
        viewBinding = true
        dataBinding = true
    }

    testOptions {
        animationsDisabled = true
        unitTests {
            returnDefaultValues true
            includeAndroidResources = true

            all {
                // set heap size for the test JVM(s)
                minHeapSize = "256m"
                maxHeapSize = "1g"
                jvmArgs '-Djdk.attach.allowAttachSelf=true'

                if (it.name == 'testDebugUnitTest') {
                    systemProperty 'debug', 'true'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation Libs.androidAppCompat
    implementation Libs.annotations

    // dependency injection
    implementation Libs.dagger
    implementation Libs.daggerAndroid
    kapt Libs.daggerCompiler
    kapt Libs.daggerProcessor

    // network
    implementation Libs.okhttp
    implementation Libs.loggingInterceptor
    implementation Libs.retrofit
    implementation Libs.converterMoshi
    implementation Libs.moshi
    implementation Libs.moshiKotlinCodegen
    // for @JsonClass(generateAdapter = true)
    implementation Libs.moshiAdapters

    // database
    kapt Libs.roomCompiler
    implementation Libs.roomKtx

    // view model
    implementation Libs.lifecycleExtensions
    kapt Libs.lifecycleCompiler
    implementation Libs.fragmentKtx

    // constraint layout
    implementation Libs.constraintLayout

    // recycler view
    implementation Libs.recyclerView
    implementation Libs.material

    // image loader
    implementation Libs.glide

    // sorted list adapter
    implementation Libs.sortedListAdapter

    // Data-binding
    implementation Libs.databinding_collectionadapter
    implementation Libs.databinding_collectionadapter_recyclerView

    // Android Gif drawable
    implementation Libs.androidGifDrawable

    // kotlin stdLib
    implementation Libs.kotlinStdLib
    // coroutines
    implementation Libs.core
    implementation Libs.rx2
    implementation Libs.android

    implementation Libs.coreKtx

    // tests
    testImplementation Libs.coreTesting
    testImplementation Libs.coroutineTest
    testImplementation Libs.jUnit
    testImplementation Libs.kotlinTest
    testImplementation(Libs.mockk)
    testImplementation Libs.mockkAndroid
    androidTestImplementation Libs.espresso
    androidTestImplementation Libs.testEspressoCore
    androidTestImplementation Libs.testExtJunit
    androidTestImplementation Libs.testRunner
}
